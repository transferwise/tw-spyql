import org.eclipse.jgit.api.errors.RefAlreadyExistsException

plugins {
    id "com.github.spotbugs" version "5.0.3" apply false
    id "idea"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'org.springframework.boot' version '2.5.8' apply false
}

idea {
    project{
        vcs = "Git"
        languageLevel = "11"
    }
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.common"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}