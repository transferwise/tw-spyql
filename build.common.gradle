apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'com.github.spotbugs'
apply plugin: 'checkstyle'

group = 'com.transferwise.common'

ext.projectName = 'Transferwise Database Connection Spy'

apply from: "$project.rootDir/build.libraries.gradle"

java {
    if (springBootVersion.startsWith("3.")) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    } else {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "$projectName",
                "Implementation-Version": archiveVersion
        )
    }
}

compileJava {
    options.encoding = 'utf-8'
    options.compilerArgs << '-parameters'
    options.compilerArgs << '-Xlint'
    options.compilerArgs << '-Xlint:-processing'

    options.fork = true
    options.forkOptions.jvmArgs << '-Xmx256m'

    javaCompiler = javaToolchains.compilerFor {
        if (springBootVersion.startsWith("3.")) {
            languageVersion = JavaLanguageVersion.of(17)
        } else {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    options.fork = true
    options.forkOptions.jvmArgs << '-Xmx256m'
}


repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    local {
        canBeResolved(false)
        canBeConsumed(false)
    }
    compileClasspath {
        extendsFrom(local)
    }
    runtimeClasspath {
        extendsFrom(local)
    }
    testCompileClasspath {
        extendsFrom(local)
    }
    testRuntimeClasspath {
        extendsFrom(local)
    }
    annotationProcessor {
        extendsFrom(local)
    }
    testAnnotationProcessor {
        extendsFrom(local)
    }
}

dependencies {
    local platform(libraries.springBootDependencies)

    compileOnly libraries.lombok
    annotationProcessor libraries.lombok
    testCompileOnly libraries.lombok
    testAnnotationProcessor libraries.lombok
    testRuntimeOnly libraries.junitPlatformLauncher

    compileOnly libraries.spotbugsAnnotations
}

test {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardError"
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

spotbugsMain {
    excludeFilter = file('../spotbugs-exclude.xml')
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugs {
    effort = "max"
    spotbugsTest.enabled = false
}

tasks.withType(Checkstyle) {
    config = resources.text.fromFile(file('../google_checks.xml'))

    maxWarnings = 0

    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    checkstyleTest.enabled = false
}

idea {
    module {
        excludeDirs += file('logs')
        excludeDirs += file('gradle')
        excludeDirs += file('build')
        excludeDirs += file('target')
        excludeDirs += file('.idea')
        excludeDirs += file('.gradle')
    }
}
